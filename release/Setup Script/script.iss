; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "DataSmith"
#define MyAppVersion "1.0"
#define MyAppPublisher "康爱营养"
#define MyAppURL "http://www.chcare.cn/"
#define MyAppExeName "DataSmith.exe" 
; 是否将.net framework打包进去
#define IncludeFramework true
#define IsExternal ""

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{D2A45316-77EC-47BC-BED5-E6DEB8C02238}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=license file.txt
InfoBeforeFile=before installation.txt
InfoAfterFile=after installation.txt
OutputDir=..\
#if IncludeFramework  
  OutputBaseFilename=DataSmith_FW  
#else  
  OutputBaseFilename=DataSmith  
#endif
SetupIconFile=setup.ico
Password=ainst
Compression=lzma
SolidCompression=yes

[Languages]
Name: "Chinese"; MessagesFile: "compiler:Languages\Chinese.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
;Source: "..\..\DataSmith\bin\DataSmith.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\..\DataSmith\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\..\DataSmith\msg\*"; DestDir: "{app}\msg"; Flags: ignoreversion recursesubdirs createallsubdirs
#if IncludeFramework  
Source: "dotNetFx40_Full_x86_x64.exe"; DestDir: "{tmp}"; Flags: ignoreversion ; Check:NeedsFramework
#endif
; 注意: 不要在任何共享系统文件上使用“Flags: ignoreversion”  


[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Tasks: desktopicon

[Run]
#if IncludeFramework
Filename: {tmp}\dotNetFx40_Full_x86_x64.exe; Parameters: "/q:a /c:""install /l /q"""; WorkingDir: {tmp}; Flags:skipifdoesntexist; StatusMsg: "Installing .NET Framework if needed"
#endif
Filename: "{app}\bin\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent


[code]
// Indicates whether .NET Framework is installed.  
function IsDotNETDetected(): boolean;
var
    success: boolean;
    install: cardinal;
begin
    success := RegQueryDWordValue(HKLM, 'SOFTWARE\Microsoft\NET Framework Setup\NDP\v4\Full', 'Install', install);
    //success := RegQueryDWordValue(HKLM, 'SOFTWARE\Microsoft\NET Framework Setup\NDP\v2.0.50727', 'Install', install);  
    Result := success and (install = 1);
end;

//Remember this method from the Files section above  
function NeedsFramework(): Boolean;
begin 
  Result := (IsDotNETDetected = false);
end;
